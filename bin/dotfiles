#!/bin/zsh

dotdir=$HOME/.dotfiles

setup_bin_dir() {
	if [[ -d $HOME/bin ]]; then
		for file in $dotdir/bin/*(.N); do
			if [[ ! -e $HOME/bin/$file:t ]]; then
				ln -s $file $HOME/bin
			fi
		done
	else
		ln -s $dotdir/bin $HOME
	fi
}

link_dotfiles_and_dirs() {
	for file in $dotdir/.*(N); do
		if [[ ! -e $HOME/$file:t ]]; then
			[[ ! $file:t == .git ]] && ln -s $file $HOME
		fi
	done
}

case $1 in
  install)
		cd $dotdir
		git submodule update --init
    setup_bin_dir
    link_dotfiles_and_dirs
    ;;
  push)
		cd $dotdir && git push
    ;;
  update)
		(cd $dotdir && git pull && git submodule update)
		[[ ! -d $HOME/bin ]] && setup_bin_dir
		link_dotfiles_and_dirs
		;;
	*)
		echo "usage: $0:t <install | push | update>"
esac